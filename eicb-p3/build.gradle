apply plugin: 'java'
apply plugin: 'eclipse'

repositories {
	mavenCentral()
}

sourceSets {
	// Make the testcases directoy the default resources tree for the 'test' source set.
	test.resources.srcDirs = ['src/test/testcases']
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = 'UTF-8'
	sourceCompatibility = '17'
}

dependencies {
	implementation 'commons-io:commons-io:2.7'
	implementation 'commons-cli:commons-cli:1.3.1'
	implementation 'com.thoughtworks.xstream:xstream:1.4.20'
	implementation 'org.xmlunit:xmlunit-parent:2.2.1'
	implementation 'org.xmlunit:xmlunit-core:2.2.1'
	implementation 'org.xmlunit:xmlunit-matchers:2.2.1'

	testImplementation 'commons-io:commons-io:2.7'
	testImplementation 'junit:junit:4.13.1'
}

tasks.register('prepareSubmission', Zip) {
	description = "Prepare your solution for submission via Moodle"

	def banner = "############################################################"
	doFirst {
		if (!project.hasProperty("GroupNumber") || GroupNumber == "_not_set_")
			throw new GradleException("""${banner}
				| Group number required for submission!
				|
				| Run this task as
				|   \$ gradle prepareSubmission -PGroupNumber=42
				| or set your group number in the file 'gradle.properties'
				|${banner}""".stripMargin())

		archiveBaseName = "eicb-p3-submission-g${GroupNumber}"
	}

	outputs.dir('build/distributions')

	from(sourceSets.main.java) {
		include('mavlc/codegen/tam/CodeGenerator.java')
		into('src/main/java')
	}

	doLast {
		println banner
		println " Please submit"
		println "   " + project.relativePath(archiveFile)
		println " via Moodle"
		println banner
	}
}

apply from: 'scripts.gradle'
